这是[新冠病毒核酸快速检测平台](#新冠病毒核酸快速检测平台)内置的[DSL](#领域特定语言)，用于根据原始数据分析得到判定结果。

## 文法（BNF）

* *全规则集* ::= { *规则集* }
* *规则集* ::= *靶标签* ":" *换行* { *规则列表* }
* *规则列表* ::= { *规则*  *换行* }
* *换行* ::= "\n" { "\n" }
* *规则* ::= *条件表达式* "=>" *结果常量*
* *表达式* ::= *条件表达式*
* *条件表达式* ::= *逻辑或表达式*
* *逻辑或表达式* ::= *逻辑与表达式*  | *逻辑或表达式*  "或" *逻辑与表达式*
* *逻辑与表达式* ::= *比较表达式* | *逻辑与表达式* "且" *比较表达式*
* *比较表达式* ::= *测试表达式* | *比较表达式* *比较运算符* *测试表达式*
* *测试表达式* ::= *访问表达式* | *测试表达式* *测试运算符* *常量*
* *访问表达式* ::= *初等表达式* | *访问表达式* "的" *属性原语*
* *初等表达式* ::= *常量* | *原语*  | *分组表达式*
* *分组表达式* ::= "(" *表达式* ")"
* *原语* ::= *对照原语* | *属性原语*
* *属性原语* ::= *计数属性原语* | *靶基因索引原语*  |"结果"|"CT"
* *计数属性原语* ::= "阳性数"|"阴性数"|"重检数"|"异常数"|
* *对照原语* ::= "阳性对照"|"阴性对照"|"非对照"
* *常量* ::= *整数常量*  | *实数常量*  | *逻辑常量* | *结果常量* 
* *整数常量* ::= [ *符号* ] 数字 { 数字 }
* *实数常量* ::= [ *符号* ] 数字 { 数字 } "." { 数字 }
* *符号* ::= "+" | "-"
* *逻辑常量* ::= "真"|"假"
* *结果常量* ::= "阳性"|"阴性"|"重检"|"异常阳性"|"异常阴性"|"异常重检"
* *靶基因索引原语* ::= "'" *靶基因标识* "'"
* *靶标签* ::= *靶基因标识* | "{" *靶基因标识序列* "}"
* *靶基因标识序列* ::= { *靶基因标识序列* "," } *靶基因标识* 
* *靶基因标识*  ::= *靶基因标识字符* { *靶基因标识字符* }
* *靶基因标识字符* ::= 字母|数字|"+"|"-"|"_"
* *比较运算符* ::= "<"|">"|"<="|">="|"="|"!="
* *测试运算符* ::= "是"|"非"

注：字母 不仅包含拉丁字母，而是包含[Unicode 字符类别](https://www.unicode.org/reports/tr44/#General_Category_Values)中的 Letter 类别。

## 语法说明

整个规则文件通常由若干个规则集构成，每个规则集指定在特定靶基因下的分析规则。

规则集对应的靶基因可以是单个也可以是一组，分析时会先尝试对原始数据的每个靶基因应用对应的单个靶基因规则，再根据原始数据所包含的靶基因，应用对应的分组靶基因规则。

规则集内通常包含若干条规则，组成一串规则列表。规则的应用是从上到下按顺序进行尝试的，当且仅当其中一条规则左侧的条件表达式求值结果为“真”时，这条规则会得到应用，并将规则右侧值作为结果。如果没有可应用的规则，那么结果为“异常重检”。由于规则应用按顺序进行，因此可以将常量“真”作为最后一条规则的条件，其结果作为最终的回退值。

### 表达式

表达式是*运算符*和它们的*操作数*构成的序列。对表达式进行求值会产生一个结果。

#### 初等表达式

任何运算符的操作数都可以是其他的表达式或初等表达式。初等表达式包括：

* 常量
  * 逻辑常量
  * 整数常量
  * 实数常量
  * 结果常量
* 原语
  * 对照原语
  * 属性原语
  * 靶基因索引原语

由括号包围的表达式（分组表达式）也被归为初等表达式：这确保了括号具有比任何运算符更高的优先级。括号保持值和类型不变。

#### 运算符

下表按优先级高低汇总了所有运算符：

| 类别 |           运算符           | 结合性 |
| :--: | :------------------------: | :----: |
| 初等 |          `(` `)`           |  匹配  |
| 访问 |            `的`            | 左结合 |
| 测试 |         `是` `非`          | 左结合 |
| 比较 | `<` `>` `<=` `>=` `=` `!=` | 左结合 |
| 逻辑 |            `且`            | 左结合 |
| 逻辑 |            `或`            | 左结合 |

注：由于规则**不是**表达式，因此 `=>` 并不算运算符。

#### 属性

属性通常需要通过访问运算符进行访问。但当访问该规则集对应的靶自身的属性时，无需通过访问运算符访问。

#### 对照原语

对照原语包含：

* 非对照
* 阳性对照
* 阴性对照

对照原语的求值结果为逻辑值（“真”或“假”）。当且仅当应用规则的样本是对应类别的对照样本时，求值结果为真。

### 类型

任何表达式都有其类型，与该表达式求值结果的类型相同。

#### 逻辑类型

逻辑类型有且仅有两种可能值：“真”和“假”。对应.NET中的[Boolean](https://docs.microsoft.com/zh-cn/dotnet/api/system.boolean)类型。

#### 结果标记类型

结果标记类型有多种可能值：“阳性”、“阴性”、“重检”，以及具有异常标记的“异常阳性”、“异常阴性”、“异常重检”。分别对应样本的几种分析结果情况。

#### 整数类型

有符号整数类型。对应.NET中的[Int32](https://docs.microsoft.com/zh-cn/dotnet/api/system.int32)类型。

#### 实数类型

浮点实数。对应.NET中的[Double](https://docs.microsoft.com/zh-cn/dotnet/api/system.double)类型。

#### 靶信息类型

靶信息类型是一种复合类型，它用于表示应用此规则时该靶基因对应的相关样本数据。

靶信息类型包含以下属性：

* “CT” - 该靶基因对应的CT值。
* “结果” - 该靶基因对应的结果值。注：在单个靶基因规则中，该属性无效。

#### 靶信息集合类型

由一组靶信息数据构成的对象的类型。在分组靶基因规则中，自身

集合类型包含以下属性：

* 阳性数
* 阴性数
* 重检数
* 异常数
* 靶基因索引

## 示例

```
Ho-RN:
非对照且CT<=38 => 阴性
阳性对照且CT<32 => 阳性
阴性对照且CT>40 => 阴性

ORF1ab:
非对照且CT<=38 => 阳性
非对照且CT>38 => 阴性
阳性对照且CT<32 => 阳性
阴性对照且CT>40 => 阴性

E:
非对照且CT<=38 => 阳性
非对照且CT>38 => 阴性
阳性对照且CT<32 => 阳性
阴性对照且CT>40 => 阴性

N:
非对照且CT<=38 => 阳性
非对照且CT>38 => 阴性
阳性对照且CT<32 => 阳性
阴性对照且CT>40 => 阴性

{ORF1ab, N, E}:
异常数>0且阳性数>=2 => 异常阳性
异常数>0且阳性数=1 => 异常重检
异常数>0且阳性数=0 => 异常阴性
阳性数>=2 => 阳性
阳性数=1 => 重检
阳性数=0 => 阴性

{Ho-RN, ORF1ab, N, E}:
'Ho-RN'的结果非阳性且异常数>0且阳性数>=2 => 异常阳性
'Ho-RN'的结果非阳性且异常数>0且阳性数=1 => 异常重检
'Ho-RN'的结果非阳性且异常数>0且阳性数=0 => 异常阴性
'Ho-RN'的结果非阳性且阳性数>=2 => 阳性
'Ho-RN'的结果非阳性且阳性数=1 => 重检
'Ho-RN'的结果非阳性且阳性数=0 => 阴性
'Ho-RN'的结果是阳性且异常数>0且阳性数>=3 => 异常阳性
'Ho-RN'的结果是阳性且异常数>0且阳性数=2 => 异常重检
'Ho-RN'的结果是阳性且异常数>0且阳性数=1 => 异常阴性
'Ho-RN'的结果是阳性且阳性数>=3 => 阳性
'Ho-RN'的结果是阳性且阳性数=2 => 重检
'Ho-RN'的结果是阳性且阳性数=1 => 阴性
```

上述规则对应的判断逻辑：

同一次实验需同时满足以下条件，否则该次扩增结果无效，所有样品需要重新进行扩增。

1. 阴性对照孔所有通道Ct值>40.
2. 阳性对照孔扩增曲线呈S型，所有通道Ct值<32

靶基因Ct值判读：

| 检测通道     | 阴性          | 阳性 |
| ------------ | ------------- | ---- |
| ORF1ab基因   | >38或无未检出 | ≤38  |
| N基因        | >38或无未检出 | ≤38  |
| E基因        | >38或无未检出 | ≤38  |
| 内参 (Ho-RN) | ≤38           | /    |

结果判定：

1. ORF1ab、N、E基因两个或两个以上为阳性则结果为阳性；
2. ORF1ab、N、E基因只有一个为阳性则结果为重检；
3. ORF1ab、N、E基因全都为阴性则结果为阴性。